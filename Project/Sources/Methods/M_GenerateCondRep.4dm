//%attributes = {"invisible":true}
If (False:C215)
	// Modified by: costasmanousakis-(Designer)-(2/26/10 17:19:43)
	Mods_2010_02
	//  `Display the Bridge Status report that was just generated
	// Modified by: costasmanousakis-(Designer)-(3/2/11 14:32:13)
	Mods_2011_03
	//  `Added prompt for designer to pick type of report to be generated
	// Modified by: Costas Manousakis-(Designer)-(5/16/18 13:34:14)
	Mods_2018_05
	//  `changes to allow for the new method of calculating SD
	//  `use new dialog "BridgeStatRepParameters"
	// Modified by: Costas Manousakis-(Designer)-(9/10/19 17:10:31)
	Mods_2019_09
	//  `open the created report in a new window
End if 


If (Records in selection:C76([Bridge MHD NBIS:1])>0)
	C_TEXT:C284($Comments_txt)
	G_InitCondRepVars
	
	STATREP_DistrictMethod_L:=2  //use the TownData District info - 6 Districts
	C_BOOLEAN:C305($OpenReport_b)
	$OpenReport_b:=False:C215
	
	If (User in group:C338(Current user:C182; "Design Access Group"))
		//defualt is the distr 6 new report
		STATREP_UseLegacyOwn_b:=False:C215
		C_BOOLEAN:C305($Poor_or_OldSD_b)
		$Poor_or_OldSD_b:=SIA_NewBridgeStatCalc
		
		If (False:C215)
			C_LONGINT:C283($Option_L)
			
			$Option_L:=ut_3Option_Confirm("What status report type?"; "Legacy"; "MDOT-5"; "MDOT-6")
			Case of 
				: ($Option_L=1)  //Legacy
					STATREP_UseLegacyOwn_b:=True:C214
					STATREP_DistrictMethod_L:=3  //`use the OldDistrictNo3 info - 5 Districts
					$Comments_txt:="Legacy Owner Report Generated By Server method."
					
				: ($Option_L=2)  //MDOT-5
					STATREP_UseLegacyOwn_b:=False:C215
					STATREP_DistrictMethod_L:=3  //`use the OldDistrictNo3 info - 5 Districts
					$Comments_txt:="Report Generated By Server method."
				: ($Option_L=3)  //MDOT-6
					STATREP_UseLegacyOwn_b:=False:C215
					STATREP_DistrictMethod_L:=2  //use the TownData District info - 6 Districts
					$Comments_txt:="Transition to 6 District Report Generated By Server method."
			End case 
			
			
		End if 
		C_OBJECT:C1216(STATREP_CalcParams_o)
		OB SET:C1220(STATREP_CalcParams_o; "DistrictCalc"; STATREP_DistrictMethod_L)
		OB SET:C1220(STATREP_CalcParams_o; "SDNewCalcMethod"; $Poor_or_OldSD_b)
		OB SET:C1220(STATREP_CalcParams_o; "UseLegacy"; STATREP_UseLegacyOwn_b)
		C_LONGINT:C283($newwin_L)
		$newwin_L:=Open form window:C675("BridgeStatRepParameters"; Movable form dialog box:K39:8)
		
		DIALOG:C40("BridgeStatRepParameters")
		CLOSE WINDOW:C154($newwin_L)
		
		STATREP_DistrictMethod_L:=OB Get:C1224(STATREP_CalcParams_o; "DistrictCalc"; Is longint:K8:6)
		$Poor_or_OldSD_b:=OB Get:C1224(STATREP_CalcParams_o; "SDNewCalcMethod"; Is boolean:K8:9)
		STATREP_UseLegacyOwn_b:=OB Get:C1224(STATREP_CalcParams_o; "UseLegacy"; Is boolean:K8:9)
		
		G_MyConfirm("Status Report Parameters : "+JSON Stringify:C1217(STATREP_CalcParams_o; *)+Char:C90(13)+" Create Report?")
		
		If (OK=1)
			$OpenReport_b:=True:C214
			GenerateCondRep(True:C214; -1; $Poor_or_OldSD_b)
			[Status Report:69]Comments:135:=$Comments_txt
			SAVE RECORD:C53([Status Report:69])
			
		End if 
		
	Else 
		$OpenReport_b:=True:C214
		
		GenerateCondRep(True:C214)
	End if 
	
	If ($OpenReport_b)
		C_LONGINT:C283($winID_L)
		$winID_L:=Open form window:C675([Status Report:69]; "I_StatusReport"; Movable form dialog box:K39:8)
		FORM SET OUTPUT:C54([Status Report:69]; "I_StatusReport")
		MODIFY RECORD:C57([Status Report:69])
		UNLOAD RECORD:C212([Status Report:69])
		
		C_TEXT:C284($msg)
		$msg:="The Bridge Status report that was just generated can also be found under the 'Br"
		$msg:=$msg+"idge Condition' menu item of  'Reports' menu from the Splash Screen."
		$msg:=$msg+" It will be one of the top listed with your name : <"+Current user:C182+">."
		ALERT:C41($msg)
		UNLOAD RECORD:C212([Status Report:69])
		CLOSE WINDOW:C154($winID_L)
	End if 
Else 
	ALERT:C41("There are no Bridge Records listed")
End if 