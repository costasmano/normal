//%attributes = {"invisible":true}
// ut_SRVMonthlyReports
// Description
// Generate PDFs of monthly reports and email them
// Parameters
// [$1] : $TestRun_txt : Optional -  Pass any string to do a test run
// ----------------------------------------------------
If (False:C215)
	// ----------------------------------------------------
	// User name (OS): charlesmiller
	// Date and time: 02/11/10, 12:54:18
	// ----------------------------------------------------
	Mods_2010_02  //r001 CJ Miller`02/11/10, 12:54:25      `Add new server processing of monthly reports
	
	// Modified by: costasmanousakis-(Designer)-(8/2/10 08:02:20)
	Mods_2010_08
	//  `Added printing of the MDOT report for 6 Districts
	//  `Changed calls to prt_substdrep with first param = longinteger
	// Modified by: costasmanousakis-(Designer)-(3/1/11 09:29:07)
	Mods_2011_03
	//  `use ut_ControlSendMail ("INIT") to initialize mail variables; 
	//  `Added optional 1st param to just run a check - pass any nonblank string to run the test
	//  `Test is basically everything as normal except the To and CC  values from the db.
	Mods_2011_03  //r002 CJ Miller`03/15/11, 12:18:14      `Add code to report errors if any occurred 
	//  `Repeat loops trying to print a report each time testing if report has been generated.
	Mods_2011_04  //r002 CJ Miller`04/11/11, 11:06:11      `Change sequence of reports
	Mods_2011_04  //r002 CJ Miller`04/12/11, 11:59:39      `Change from
	//Get indexed string to substring of date;5 to get 3 character month
	Mods_2011_05  //r002 CJ Miller`05/20/11, 11:27:12      `Change directory where reports are stored to the users documents folder
	Mods_2012_09  //r001 `Add code to check for missing document. If all found no email sent 
	//Modified by: Charles Miller (9/11/12 10:58:17)
	// Modified by: Costas Manousakis-(Designer)-(3/13/13 14:15:09)
	Mods_2013_03
	//  `Added  STATREP_ENGLISH_b:= true for new SD by deck area reports
	// Modified by: Costas Manousakis-(Designer)-(5/1/14 11:51:14)
	Mods_2014_05_bug
	//  `Removed the printing of the MassDOT 5 Dist version of the bridge report
	// Modified by: Costas Manousakis-(Designer)-(8/05/16 )
	Mods_2016_08_bug
	//  `` set to report in english units
	// Modified by: Costas Manousakis-(Designer)-(5/22/18 12:12:31)
	Mods_2018_05
	//  `changes to reports - send only the MADOT report - no more legacy
	// Modified by: Costas Manousakis-(Designer)-(9/3/20 10:22:20)
	Mods_2020_09_bug
	//  `change the message folder name to include the server IP and port number
	// Modified by: Costas Manousakis-(Designer)-(2021-12-15T00:00:00 19:26:35)
	Mods_2021_12_bug
	//  `for Windows set Current printer to generic pdf driver
End if 
//TRACE
C_TEXT:C284($TestRun_txt)
$TestRun_txt:=""
If (Count parameters:C259>0)
	C_TEXT:C284($1)
	If ($1#"")
		$TestRun_txt:="TEST"
	End if 
End if 
C_BOOLEAN:C305($NotAllDocumentsFound_b)
$NotAllDocumentsFound_b:=False:C215
C_BOOLEAN:C305(SRVReportErrror_b)
C_TEXT:C284(ReportError_txt)
ReportError_txt:=""
ON ERR CALL:C155("ut_prtSubrepError")
G_InitCondRepVars

ut_ControlSendMail("INIT")

C_DATE:C307($ReportDate_d)
$ReportDate_d:=Current date:C33(*)
C_OBJECT:C1216($serverinfo_o)
$serverinfo_o:=SYSUTIL_GetServerInfo
C_TEXT:C284($messageDir_txt)
$messageDir_txt:="Monthly_Reports_"+Replace string:C233(OB Get:C1224($serverinfo_o; "ServerIP"; Is text:K8:3); "."; "_")+"_"+OB Get:C1224($serverinfo_o; "ServerPort"; Is text:K8:3)
C_TEXT:C284($Path_txt)
$Path_txt:=ut_ReturnUserDocFolder($messageDir_txt)

READ ONLY:C145([Status Report:69])
SET QUERY DESTINATION:C396(Into set:K19:2; "ReportSet")
QUERY:C277([Status Report:69]; [Status Report:69]Date_Created:1=$ReportDate_d)
SET QUERY DESTINATION:C396(Into current selection:K19:1)

C_TEXT:C284($ReportDate_txt; $FileName_txt; $ReportMonth_txt)
$ReportMonth_txt:=Substring:C12(String:C10($ReportDate_d; Internal date long:K1:5); 1; 3)
$ReportDate_txt:=String:C10(Day of:C23($ReportDate_d); "00")+"-"+$ReportMonth_txt+"-"+String:C10(Year of:C25($ReportDate_d))

If (False:C215)  //Skip this for now
	USE SET:C118("ReportSet")
	QUERY SELECTION:C341([Status Report:69]; [Status Report:69]Comments:135="Legacy Owner Report Generated By Server method@"; *)
	QUERY SELECTION:C341([Status Report:69];  | ; [Status Report:69]Comments:135="Report Generated By Server method@")
	SR_ControlQuarterlyPrint("OUT")  //ok lets run quarterly status reports
End if 

STATREP_ENGLISH_b:=True:C214  // set to report in english units

If (False:C215)
	USE SET:C118("ReportSet")
	
	QUERY SELECTION:C341([Status Report:69]; [Status Report:69]Comments:135="Report Generated By Server method@")
	$FileName_txt:="Bridge Status (5Dist) "+$ReportDate_txt+".pdf"
	$FileName_txt:=$Path_txt+$FileName_txt
	C_BOOLEAN:C305($PDFReportFound_b)
	C_LONGINT:C283($Count_l)
	SET PRINT OPTION:C733(Destination option:K47:7; 3; $FileName_txt)
	$PDFReportFound_b:=False:C215
	$Count_l:=0
	Repeat 
		prt_substdrep(1; True:C214; $FileName_txt)  //print the  massdot_5 report in batch mode...
		If (Test path name:C476($FileName_txt)=Is a document:K24:1)
			$PDFReportFound_b:=True:C214
			APPEND TO ARRAY:C911(MAILAttachments_atxt; $FileName_txt)
		Else 
			$Count_l:=$Count_l+1
			DELAY PROCESS:C323(Current process:C322; 60*30)  //delay 30 seconds
		End if 
		
	Until (($Count_l>5) | ($PDFReportFound_b))
	
	If (SRVReportErrror_b) | ($Count_l>0)
		ReportError_txt:=ReportError_txt+"finished prt_substdrep (1;True) "+Char:C90(Carriage return:K15:38)
		If (Test path name:C476($FileName_txt)=Is a document:K24:1)
			ReportError_txt:=ReportError_txt+$FileName_txt+" created. Tried "+String:C10($Count_l)+" times"+Char:C90(Carriage return:K15:38)
		Else 
			$NotAllDocumentsFound_b:=True:C214
			ReportError_txt:=ReportError_txt+$FileName_txt+" not created. Tried "+String:C10($Count_l)+" times"+Char:C90(Carriage return:K15:38)
		End if 
		
	End if 
	
End if 

USE SET:C118("ReportSet")
QUERY SELECTION:C341([Status Report:69]; [Status Report:69]Comments:135="Transition to 6 District Report Generated By Server method@")
$FileName_txt:="Bridge Status (6Dist) "+$ReportDate_txt+".pdf"
$FileName_txt:=$Path_txt+$FileName_txt
If (Is Windows:C1573)
	SET CURRENT PRINTER:C787(Generic PDF driver:K47:15)
End if 
SET PRINT OPTION:C733(Destination option:K47:7; 3; $FileName_txt)
$PDFReportFound_b:=False:C215
$Count_l:=0
Repeat 
	
	prt_substdrep(0; True:C214; $FileName_txt)  //print the  massdot 6 district report in batch mode use default form
	
	If (Test path name:C476($FileName_txt)=Is a document:K24:1)
		$PDFReportFound_b:=True:C214
		APPEND TO ARRAY:C911(MAILAttachments_atxt; $FileName_txt)
	Else 
		$Count_l:=$Count_l+1
		DELAY PROCESS:C323(Current process:C322; 60*30)  //delay 30 seconds
	End if 
	
Until (($Count_l>5) | ($PDFReportFound_b))

If (SRVReportErrror_b) | ($Count_l>0)
	ReportError_txt:=ReportError_txt+"finished prt_substdrep (3;True) "+Char:C90(Carriage return:K15:38)
	If (Test path name:C476($FileName_txt)=Is a document:K24:1)
		ReportError_txt:=ReportError_txt+$FileName_txt+" created. Tried "+String:C10($Count_l)+" times"+Char:C90(Carriage return:K15:38)
	Else 
		$NotAllDocumentsFound_b:=True:C214
		ReportError_txt:=ReportError_txt+$FileName_txt+" not created. Tried "+String:C10($Count_l)+" times"+Char:C90(Carriage return:K15:38)
	End if 
End if 

If (False:C215)
	
	USE SET:C118("ReportSet")
	QUERY SELECTION:C341([Status Report:69]; [Status Report:69]Comments:135="Legacy Owner Report Generated By Server method@")
	$FileName_txt:="Bridge Status (MHD) "+$ReportDate_txt+".pdf"
	$FileName_txt:=$Path_txt+$FileName_txt
	
	SET PRINT OPTION:C733(Destination option:K47:7; 3; $FileName_txt)
	$PDFReportFound_b:=False:C215
	$Count_l:=0
	
	Repeat 
		
		prt_substdrep(0; True:C214; $FileName_txt)  //print the legacy report in batch mode.use default form
		If (Test path name:C476($FileName_txt)=Is a document:K24:1)
			$PDFReportFound_b:=True:C214
			APPEND TO ARRAY:C911(MAILAttachments_atxt; $FileName_txt)
		Else 
			$Count_l:=$Count_l+1
			DELAY PROCESS:C323(Current process:C322; 60*30)  //delay 30 seconds
		End if 
		
	Until (($Count_l>5) | ($PDFReportFound_b))
	
	If (SRVReportErrror_b) | ($Count_l>0)
		ReportError_txt:=ReportError_txt+"finished prt_substdrep (2;True) "+Char:C90(Carriage return:K15:38)
		If (Test path name:C476($FileName_txt)=Is a document:K24:1)
			ReportError_txt:=ReportError_txt+$FileName_txt+" created. Tried "+String:C10($Count_l)+" times"+Char:C90(Carriage return:K15:38)
		Else 
			$NotAllDocumentsFound_b:=True:C214
			ReportError_txt:=ReportError_txt+$FileName_txt+" not created. Tried "+String:C10($Count_l)+" times"+Char:C90(Carriage return:K15:38)
		End if 
	End if 
	
End if 

If (Is Windows:C1573)
	SET CURRENT PRINTER:C787("")
End if 

READ WRITE:C146([Status Report:69])

//Set default values for the email
<>SHOWERRORS:=False:C215
tFromEmailAddress:="Manousakis Costas (DOT) <costas.manousakis@dot.state.ma.us>"
tToBuilt_txt:="costas.manousakis@dot.state.ma.us,Charles Miller <cjmiller@informed-solutions.com"+">"

If (SRVReportErrror_b) | ($NotAllDocumentsFound_b)
	
	ARRAY TEXT:C222($MailAttachments_atxt; 0)
	
	COPY ARRAY:C226(MAILAttachments_atxt; $MailAttachments_atxt)
	ARRAY TEXT:C222(MAILAttachments_atxt; 0)
	tSubject:="Diagnostic report during report processing"
	tMailNote:=ReportError_txt
	If (ut_ControlSendMail)
		
	End if 
	COPY ARRAY:C226($MAILAttachments_atxt; MailAttachments_atxt)
	
End if 

ARRAY TEXT:C222(MAILHeaderTypes_atxt; 2)
MAILHeaderTypes_atxt{1}:="Content-Type"
MAILHeaderTypes_atxt{2}:="Disposition-Notification-To"
ARRAY TEXT:C222(MAILHeaderValues_atxt; 2)
MAILHeaderValues_atxt{1}:="text/plain"
MAILHeaderValues_atxt{2}:=tFromEmailAddress
tSubject:="Bridge Status "+$ReportDate_txt
tMailNote:=Char:C90(13)+Char:C90(13)+"Attached please find the Bridge Status Report for "+$ReportDate_txt+"."
tMailNote:=tMailNote+Char:C90(13)+Char:C90(13)+(30*"-")+Char:C90(13)
tMailNote:=tMailNote+"Automated e-mail sent by the MassDOT BMS system."+Char:C90(13)
tMailNote:=tMailNote+(30*"-")

tFromEmailAddress:=ut_GetSysParameter("MAIL_BRGSTATFROM"; tFromEmailAddress)
If (Not:C34($TestRun_txt="TEST"))
	tToBuilt_txt:=ut_GetSysParameter("MAIL_BRGSTATLIST"; tToBuilt_txt)
	tCCBuilt_txt:=ut_GetSysParameter("MAIL_BRGSTATCC"; tCCBuilt_txt)
End if 
tSubject:=ut_GetSysParameter("MAIL_BRGSTATSUBJ"; tSubject)
tMailNote:=ut_GetSysParameter("MAIL_BRGSTATBODY"; tMailNote)

tSubject:=Replace string:C233(tSubject; "<V_REPORT_DATE>"; $ReportDate_txt)
tMailNote:=Replace string:C233(tMailNote; "<V_REPORT_DATE>"; $ReportDate_txt)

If (ut_ControlSendMail)
	
End if 
ON ERR CALL:C155("")
//End ut_SRVMonthlyReports