//%attributes = {"invisible":true}
//Method: BrgRank_ImportRankCalc
//Description
// Import Bridge rank calculation results from a text results file generated by`
// method BrgRank_DoRankCalc 
// Parameters
// ----------------------------------------------------
If (False:C215)
	// ----------------------------------------------------
	//User name (OS): Costas Manousakis
	//User (4D) : Designer
	//Created : 
	//Date and time: Jun7, 2023, 18:10:48
	Mods_2023_06_bug
	// ----------------------------------------------------
	
End if 
//
// select rank calc results text file
C_TEXT:C284($reportfile_txt)
$reportfile_txt:=Select document:C905(""; ".txt"; "Select results file from a run by Bridge Rank Calculation"; 0)

If (OK=1)
	C_OBJECT:C1216($file_o)
	
	$file_o:=File:C1566(Document; fk platform path:K87:2)
	C_TEXT:C284($report_txt)
	$report_txt:=$file_o.getText()
	
	
	Case of 
		: ($report_txt="")
			ALERT:C41("There was no text in selected file")
			
		: ($report_txt#"Bridge Rank Calc date :@")
			//1st line must start with "Bridge Rank Calc date :"
			ALERT:C41("File does not start with \"Bridge Rank Calc date  :\"")
			
		Else 
			
			C_LONGINT:C283($progress_L)
			$progress_L:=Progress New
			Progress SET TITLE($progress_L; "Bridge Rank Calculation import"; -1)
			
			C_COLLECTION:C1488($lines_c)
			
			$lines_c:=Split string:C1554($report_txt; Char:C90(Carriage return:K15:38); sk ignore empty strings:K86:1)
			C_TEXT:C284($line_txt)
			C_LONGINT:C283($headerline_L; $line_l)
			C_TEXT:C284($datasource_txt; $archivereason_txt)
			C_DATE:C307($calcdate_d; $archivedate_d; $pontismetricdate_d)
			$calcdate_d:=Date:C102($lines_c[0])
			$datasource_txt:=Replace string:C233($lines_c[1]; "Data Source :\t"; "")
			
			If ($datasource_txt="Bridge archive table@")
				$datasource_txt:="ARCHIVE"
				$archivedate_d:=Date:C102($lines_c[2])
				$archivereason_txt:=Replace string:C233($lines_c[3]; "ARCHIVE Purpose :\t"; "")
				$pontismetricdate_d:=Date:C102($lines_c[4])
				$headerline_L:=5
			Else 
				$datasource_txt:="BRIDGE"
				$pontismetricdate_d:=Date:C102($lines_c[2])
				$headerline_L:=3
			End if 
			
			C_OBJECT:C1216($bridgeranks_o)
			$bridgeranks_o:=ds:C1482.BridgeRankCalc.query("RunDate_d = :1 & "+\
				"DataSource_s = :2 & "+\
				"ArchivePurpose_s = :3 & "+\
				"ArchiveDate_d = :4 & "+\
				"PontisMetricDate_d = :5"; \
				$calcdate_d; $datasource_txt; $archivereason_txt; $archivedate_d; $pontismetricdate_d)
			
			C_BOOLEAN:C305($continue_b)
			C_LONGINT:C283($BridgeRankRunID_L)
			If ($bridgeranks_o.length>0)
				G_MyConfirm("A bridge rank calculation with the same date exists already \n"+\
					"Database data :\n"+\
					"Data source "+$bridgeranks_o[0].DataSource_s+"\n"+\
					"Archive info "+$bridgeranks_o[0].ArchivePurpose_s+" - "+String:C10($bridgeranks_o[0].ArchiveDate_d)+"\n"+\
					"Pontis metric date "+String:C10($bridgeranks_o[0].PontisMetricDate_d)+"\n"+\
					"Report file data :\n"+\
					"Data source "+$datasource_txt+"\n"+\
					"Archive info "+$archivereason_txt+" - "+String:C10($archivedate_d)+"\n"+\
					"Pontis metric date "+String:C10($pontismetricdate_d)+"\n"+\
					"Replace calculation data or Cancel import?"; "Replace"; "Cancel")
				$continue_b:=(OK=1)
				
				If ($continue_b)
					// delete all records in [BridgeRankResult]
					C_OBJECT:C1216($notdropped_o)
					Progress SET MESSAGE($progress_L; "Deleting existing calculations")
					$notdropped_o:=ds:C1482.BridgeRankResult.query("RunID_L = :1"; $bridgeranks_o[0].RunID_L).drop(dk stop dropping on first error:K85:26)
					If ($notdropped_o.length=0)
						$BridgeRankRunID_L:=$bridgeranks_o[0].RunID_L
					Else 
						ALERT:C41("Problem deleting all Bridge rank calculation records! Import canceled!")
						$continue_b:=False:C215
					End if 
				End if 
				
			Else 
				Progress SET MESSAGE($progress_L; "Creating new set")
				Inc_Sequence("BridgeRankCalc"; ->$BridgeRankRunID_L)
				C_OBJECT:C1216($bridgerank_o)
				$bridgerank_o:=ds:C1482.BridgeRankCalc.new()
				$bridgerank_o.RunDate_d:=$calcdate_d
				$bridgerank_o.DataSource_s:=$datasource_txt
				$bridgerank_o.ArchivePurpose_s:=$archivereason_txt
				$bridgerank_o.ArchiveDate_d:=$archivedate_d
				$bridgerank_o.PontisMetricDate_d:=$pontismetricdate_d
				$bridgerank_o.RunID_L:=$BridgeRankRunID_L
				$bridgerank_o.save()
				$continue_b:=True:C214
				
			End if 
			
			If ($continue_b)
				
				//Parse header line
				C_COLLECTION:C1488($Columns_c)
				$Columns_c:=Split string:C1554($lines_c[$headerline_L]; "\t"; sk trim spaces:K86:2)
				C_LONGINT:C283($BIN_L; $AvgCondition_L; $ConditionLost_L; $HIChangePct_L; $ScourCritical_L; $AdjHIChange_L; $ADTValue_L; \
					$DetourValue_L; $ClassValue_L; $StructEvalValue_L; $DeckValue_L; $HWYEvalFactor_L; $BridgeEvalFactor_L; \
					$RankFactor_L; $Rank_L)
				$BIN_L:=$Columns_c.indexOf("BIN")
				$AvgCondition_L:=$Columns_c.indexOf("AverageCondition")
				$ConditionLost_L:=$Columns_c.indexOf("ConditionLost")
				$HIChangePct_L:=$Columns_c.indexOf("HIChange")
				$ScourCritical_L:=$Columns_c.indexOf("ScourCriticalCode")
				$AdjHIChange_L:=$Columns_c.indexOf("AdjustedHIChange")
				$ADTValue_L:=$Columns_c.indexOf("ADT Value")
				$DetourValue_L:=$Columns_c.indexOf("Detour Value")
				$ClassValue_L:=$Columns_c.indexOf("Class Value")
				$StructEvalValue_L:=$Columns_c.indexOf("StructEvalValue")
				$DeckValue_L:=$Columns_c.indexOf("Deck Value")
				$HWYEvalFactor_L:=$Columns_c.indexOf("HWYEval")
				$BridgeEvalFactor_L:=$Columns_c.indexOf("HWY Eval Factor")
				$RankFactor_L:=$Columns_c.indexOf("Rank Factor")
				$Rank_L:=$Columns_c.indexOf("4D FinalRank")
				//load data rows
				C_COLLECTION:C1488($Data_c)
				$Data_c:=New collection:C1472
				Progress SET MESSAGE($progress_L; "Building data")
				For ($line_l; $headerline_L+1; ($lines_c.length-1))
					$Columns_c:=Split string:C1554($lines_c[$line_l]; "\t")
					$Data_c.push(New object:C1471("BIN"; $Columns_c[$BIN_L]; \
						"AvgCondition_r"; Num:C11($Columns_c[$AvgCondition_L]); \
						"ConditionLost_r"; Num:C11($Columns_c[$ConditionLost_L]); \
						"HIChangePct_r"; Num:C11($Columns_c[$HIChangePct_L]); \
						"ScourCritical_s"; $Columns_c[$ScourCritical_L]; \
						"AdjHIChange_r"; Num:C11($Columns_c[$AdjHIChange_L]); \
						"ADTValue_r"; Num:C11($Columns_c[$ADTValue_L]); \
						"DetourValue_r"; Num:C11($Columns_c[$DetourValue_L]); \
						"ClassValue_r"; Num:C11($Columns_c[$ClassValue_L]); \
						"StructEvalValue_r"; Num:C11($Columns_c[$StructEvalValue_L]); \
						"DeckValue_r"; Num:C11($Columns_c[$DeckValue_L]); \
						"HWYEvalFactor_r"; Num:C11($Columns_c[$HWYEvalFactor_L]); \
						"BridgeEvalFactor_r"; Num:C11($Columns_c[$BridgeEvalFactor_L]); \
						"RankFactor_r"; Num:C11($Columns_c[$RankFactor_L]); \
						"Rank_L"; Num:C11($Columns_c[$Rank_L]); \
						"RunID_L"; $BridgeRankRunID_L; \
						"UniqueKey_s"; $Columns_c[$BIN_L]+String:C10($BridgeRankRunID_L; (11*"0"))))
				End for 
				C_OBJECT:C1216($bridgerankcalcres_o)
				Progress SET MESSAGE($progress_L; "Saving data to database")
				$bridgerankcalcres_o:=ds:C1482.BridgeRankResult.fromCollection($Data_c)
			End if 
			
			Progress QUIT($progress_L)
			
			ALERT:C41("Finished importing Bridge Rank calculations")
			
	End case 
	
End if   // if file selected

//End BrgRank_ImportRankCalc   