//%attributes = {"invisible":true}
//G_GetElmtLabel
// $0 : Element label used as a header in the comments, and in the list of special member elements pages 1-2
// $1 : $GetParentHeader_b (optional)
If (False:C215)
	
	// Modified by: costasmanousakis-(Designer)-(1/11/2007 12:35:50)
	Mods_2007_CM04
	// Modified by: costasmanousakis-(Designer)-(12/20/07 12:04:28)
	Mods_2007_CM_5401
	//  `Use the ◊ELMTDICT_@ arrays instead of the table and pushing poping records for parent.
	// Modified by: costasmanousakis-(Designer)-(3/10/09 14:07:37)
	Mods_2009_03
	//Use the element arrays generated by INSP_ElmtDescUtil to get the parent description.
	// Modified by: Costas Manousakis-(Designer)-(12/12/12 08:26:25)
	Mods_2012_12
	//  `Use arrays ◊ALLElmtCatCode_as, ◊ALLElmtCatDesc_as instead of querying [Element Cat]
	// Modified by: Costas Manousakis-(Designer)-(5/2/13 15:23:27)
	Mods_2013_05
	//  `Added special for Tunnel items 62a-g
	Mods_2013_05  //r003 ` Make 62 b Roadway instead of structural
	//Modified by: Charles Miller (5/21/13 13:15:57)
	// Modified by: Costas Manousakis-(Designer)-(12/9/13 16:53:03)
	Mods_2013_12
	//  `For Tunnel categories remove the descr of the category from the title ; left only 62a,b,c, etc.
	//  `Also add a "." between the itemtitle and the element title
End if 
C_BOOLEAN:C305($1; $GetParentHeader_b)
C_TEXT:C284($vElmTitle; $vItmtitle; $vParentTitle; $0)  // Command Replaced was o_C_STRING length was 132
C_LONGINT:C283($vLen)  //Command Replaced was o_C_INTEGER
C_LONGINT:C283(INSP_PrevElmtParntID)
If (Count parameters:C259=1)
	$GetParentHeader_b:=$1
Else 
	$GetParentHeader_b:=False:C215
End if 
C_LONGINT:C283($ElmIndx_L; $ElmParIndx_L; $catIndx_L)
$ElmIndx_L:=Find in array:C230(<>ELMTDICT_ELNum_ai; [ElementsSafety:29]ElementNo:1)

Case of 
	: (Substring:C12(<>ELMTDICT_Cat_as{$ElmIndx_L}; 1; 1)="N")
		$vItmtitle:="Item "+Substring:C12(<>ELMTDICT_Cat_as{$ElmIndx_L}; 2)
	: (<>ELMTDICT_Cat_as{$ElmIndx_L}="TRA")
		$vItmtitle:="Item 36"
	: (<>ELMTDICT_Cat_as{$ElmIndx_L}="STR")
		$vItmtitle:="62a"
	: (<>ELMTDICT_Cat_as{$ElmIndx_L}="ROA")
		$vItmtitle:="62b"
	: (<>ELMTDICT_Cat_as{$ElmIndx_L}="CEI")
		$vItmtitle:="62c"
	: (<>ELMTDICT_Cat_as{$ElmIndx_L}="SUP")
		$vItmtitle:="62d"
	: (<>ELMTDICT_Cat_as{$ElmIndx_L}="CRO")
		$vItmtitle:="62e"
	: (<>ELMTDICT_Cat_as{$ElmIndx_L}="EGR")
		$vItmtitle:="62f"
	: (<>ELMTDICT_Cat_as{$ElmIndx_L}="UTI")
		$vItmtitle:="62g"
	Else 
		$catIndx_L:=Find in array:C230(<>ALLElmtCatCode_as; <>ELMTDICT_Cat_as{$ElmIndx_L})
		
		If ($catIndx_L>0)
			$vItmtitle:=<>ALLElmtCatDesc_as{$catIndx_L}
		Else 
			$vItmtitle:=<>ELMTDICT_Cat_as{$ElmIndx_L}
		End if 
		
End case 
//Add Description of parent item if any      
If (<>ELMTDICT_Parent_ai{$ElmIndx_L}>0)
	$ElmParIndx_L:=Find in array:C230(<>ELMTDICT_ELNum_ai; <>ELMTDICT_Parent_ai{$ElmIndx_L})
	$vElmTitle:=<>ELMTDICT_Prefix_as{$ElmParIndx_L}
	//do this only if the arrays have been defined and are non-zero
	C_LONGINT:C283($varType; $ElmParActIndx_L)
	$varType:=Type:C295(INSPPR_ElmNo_ai)
	$ElmParActIndx_L:=-1
	If ($varType=Integer array:K8:18)
		If (Size of array:C274(INSPPR_ElmNo_ai)>0)
			$ElmParActIndx_L:=Find in array:C230(INSPPR_ElmNo_ai; <>ELMTDICT_Parent_ai{$ElmIndx_L})
		End if 
	End if 
	If ($ElmParActIndx_L>0)
		$vParentTitle:=INSPPR_ElmDesc_atxt{$ElmParActIndx_L}
	Else 
		$vParentTitle:=<>ELMTDICT_Txt_as{$ElmParIndx_L}
	End if 
	
	If ((f_Insp_Revision3([Inspections:27]Insp Date:78)) & $GetParentHeader_b)
		If (<>ELMTDICT_Parent_ai{$ElmIndx_L}#INSP_PrevElmtParntID)
			$vElmtitle:=Replace string:C233($vElmtitle; " "; "")
			$vLen:=Length:C16($vElmTitle)
			If (Substring:C12($vElmTitle; $vLen; 1)=".")
				$vElmTitle:=Substring:C12($vElmTitle; 1; ($vLen-1))
			End if 
			$vItmtitle:=$vItmtitle+"."+$vElmtitle+" - "+$vParentTitle+Char:C90(13)+$vItmtitle
		End if 
		
	End if 
	INSP_PrevElmtParntID:=<>ELMTDICT_Parent_ai{$ElmIndx_L}
	
Else 
	INSP_PrevElmtParntID:=0
End if 

If (<>ELMTDICT_IsPar_ab{$ElmIndx_L})
	INSP_PrevElmtParntID:=<>ELMTDICT_ELNum_ai{$ElmIndx_L}
End if 

$vElmTitle:=GetElmtNumber
$vElmTitle:=Replace string:C233($vElmtitle; " "; "")
$vLen:=Length:C16($vElmTitle)
If (Substring:C12($vElmTitle; $vLen; 1)=".")
	$vElmTitle:=Substring:C12($vElmTitle; 1; ($vLen-1))
End if 
$vElmTitle:=$vElmTitle+" - "+[ElementsSafety:29]Description:22
Case of 
	: (Substring:C12(<>ELMTDICT_Cat_as{$ElmIndx_L}; 1; 1)="N")
		$0:=$vItmTitle+"."+$vElmTitle
	: (<>ELMTDICT_Cat_as{$ElmIndx_L}="TRA")
		$0:=$vItmTitle+$vElmTitle
	: (Position:C15(<>ELMTDICT_Cat_as{$ElmIndx_L}; ";STR;ROA;CEI;SUP;CRO;EGR;UTI;")>0)
		$0:=$vItmTitle+"."+$vElmTitle
	Else 
		$0:=$vItmTitle+" "+$vElmTitle
End case 