//%attributes = {"invisible":true}
//Method: PON_ELEMTSTATEQ_Entry
//Description
//
// Parameters
// ----------------------------------------------------
If (False:C215)
	// ----------------------------------------------------
	//User name (OS): Costas Manousakis
	//User (4D) : Designer
	//Date and time: 02/19/15, 17:17:14
	// ----------------------------------------------------
	//Created : 
	Mods_2015_02
End if 
//
//Â called from object method of [PON_ELEM_INSP]ELEM_QTYSTATE@ on data change
Case of 
	: (Form event code:C388=On Load:K2:1)
		
	: (Form event code:C388=On Data Change:K2:15)
		If (PON_ElemNoQuantState1_b)
			// make sure state1 is zero
			[PON_ELEM_INSP:179]ELEM_QTYSTATE1:13:=0
			If ([PON_ELEM_INSP:179]ELEM_QTYSTATE3:15+[PON_ELEM_INSP:179]ELEM_QTYSTATE4:16)>[PON_ELEM_INSP:179]ELEM_QUANTITY:7
				ALERT:C41("Cannot enter more than the Total Quantity in State 3,4")
			Else 
				[PON_ELEM_INSP:179]ELEM_QTYSTATE2:14:=[PON_ELEM_INSP:179]ELEM_QUANTITY:7-[PON_ELEM_INSP:179]ELEM_QTYSTATE3:15-[PON_ELEM_INSP:179]ELEM_QTYSTATE4:16
				[PON_ELEM_INSP:179]ELEM_PCTSTATE3:11:=Round:C94(100*[PON_ELEM_INSP:179]ELEM_QTYSTATE3:15/[PON_ELEM_INSP:179]ELEM_QUANTITY:7; PON_RoundP_L)
				[PON_ELEM_INSP:179]ELEM_PCTSTATE4:12:=Round:C94(100*[PON_ELEM_INSP:179]ELEM_QTYSTATE4:16/[PON_ELEM_INSP:179]ELEM_QUANTITY:7; PON_RoundP_L)
				[PON_ELEM_INSP:179]ELEM_PCTSTATE2:10:=100-[PON_ELEM_INSP:179]ELEM_PCTSTATE3:11-[PON_ELEM_INSP:179]ELEM_PCTSTATE4:12
			End if 
		Else 
			If ([PON_ELEM_INSP:179]ELEM_QTYSTATE2:14+[PON_ELEM_INSP:179]ELEM_QTYSTATE3:15+[PON_ELEM_INSP:179]ELEM_QTYSTATE4:16)>[PON_ELEM_INSP:179]ELEM_QUANTITY:7
				ALERT:C41("Cannot enter more than the Total Quantity in State 2,3,4")
			Else 
				[PON_ELEM_INSP:179]ELEM_QTYSTATE1:13:=[PON_ELEM_INSP:179]ELEM_QUANTITY:7-[PON_ELEM_INSP:179]ELEM_QTYSTATE2:14-[PON_ELEM_INSP:179]ELEM_QTYSTATE3:15-[PON_ELEM_INSP:179]ELEM_QTYSTATE4:16
				[PON_ELEM_INSP:179]ELEM_PCTSTATE2:10:=Round:C94(100*[PON_ELEM_INSP:179]ELEM_QTYSTATE2:14/[PON_ELEM_INSP:179]ELEM_QUANTITY:7; PON_RoundP_L)
				[PON_ELEM_INSP:179]ELEM_PCTSTATE3:11:=Round:C94(100*[PON_ELEM_INSP:179]ELEM_QTYSTATE3:15/[PON_ELEM_INSP:179]ELEM_QUANTITY:7; PON_RoundP_L)
				[PON_ELEM_INSP:179]ELEM_PCTSTATE4:12:=Round:C94(100*[PON_ELEM_INSP:179]ELEM_QTYSTATE4:16/[PON_ELEM_INSP:179]ELEM_QUANTITY:7; PON_RoundP_L)
				[PON_ELEM_INSP:179]ELEM_PCTSTATE1:9:=100-[PON_ELEM_INSP:179]ELEM_PCTSTATE2:10-[PON_ELEM_INSP:179]ELEM_PCTSTATE3:11-[PON_ELEM_INSP:179]ELEM_PCTSTATE4:12
			End if 
		End if 
End case 

//End PON_ELEMTSTATEQ_Entry